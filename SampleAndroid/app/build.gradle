apply plugin: 'com.android.application'

// You can configure your environment variables in gradle.properties,
// or you can set them by overriding the default values below.
if (!project.hasProperty('GALDR_HOST')) {
    ext {
        GALDR_HOST = "http://192.168.1.32:8777"
    }
}
if (!project.hasProperty('GALDR_API_KEY')) {
    ext {
        GALDR_API_KEY = "invalid_key"
    }
}

android {
    compileSdkVersion project.TARGET_ANDROID_SDK_VERSION.toInteger()
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.vipaar.lime.samplehl"
        minSdkVersion 21
        targetSdkVersion project.TARGET_ANDROID_SDK_VERSION.toInteger()
        versionCode 1
        versionName "1.0"
        buildConfigField("String", "GALDR_HOST", "\"$GALDR_HOST\"")
        buildConfigField("String", "GALDR_API_KEY", "\"$GALDR_API_KEY\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        devasia {
            initWith debug
            matchingFallbacks = ['debug', 'release']
        }
    }

    flavorDimensions("version")
    productFlavors {
        light
    }
}

dependencies {
    def sdk_lite_version = '1.16.2'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.vipaar.lime:hlsdk-light:$sdk_lite_version"

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'

    implementation "com.android.volley:volley:1.2.1"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation('androidx.test.ext:junit:1.1.3') {
        exclude module: 'support-annotations'
    }
    implementation 'org.jdeferred.v2:jdeferred-android:2.0.0'
}
